%Note that includes are defined relative to the prove_properties.sh script
include('axioms/pairs.ax').
%-- List definitions heavily inspired from: https://tptp.org/cgi-bin/SeeTPTP?Category=Problems&Domain=DAT&File=DAT081_1.p 
tff(list,type,
    list: $tType ).

tff(nil,type,
    nil: list ).

tff(cons,type,
    cons: ( pair * list ) > list ).

tff(head,type,
    head: list > pair ).

tff(tail,type,
    tail: list > list ).

%----Selectors
tff(head_def,axiom,
    ! [P: pair,L: list] : ( head(cons(P,L)) = P ) ).

tff(tail_def,axiom,
    ! [P: pair,L: list] : ( tail(cons(P,L)) = L ) ).

%----Constructors
tff(list_constructors,axiom,
    ! [L: list] :
      ( ( L = nil )
      | ( L = cons(head(L),tail(L)) ) ) ).

tff(cons_requirement,axiom,
    ! [P: pair,L: list] : ( cons(P,L) != nil ) ).

%----Length
tff(length_l,type,
    length_l: list > $int ).

tff(length_l_def_nil,axiom,
    length_l(nil) = 0 ).

tff(length_l_def_cons,axiom,
    ! [P: pair,L: list] : ( length_l(cons(P,L)) = $sum(1,length_l(L)) ) ).

%----Append
tff(append,type,
    append: ( list * list ) > list ).

tff(append_def_one,axiom,
    ! [L: list] : ( append(nil,L) = L ) ).

tff(append_def_two,axiom,
    ! [P: pair,L: list,M: list] : ( append(cons(P,L),M) = cons(P,append(L,M)) ) ).

%----Remove
tff(remove_l,type,
    remove_l: ( list * $int ) > list ).

tff(remove_nil,axiom,
    ! [I: $int] : ( remove_l(nil,I) = nil ) ).

tff(remove_from_list,axiom,
    ! [L: list,I: $int] :
      ( ( L != nil )
     => ( remove_l(L,I) = $ite(get_left(head(L)) = I,tail(L),cons(head(L),remove_l(tail(L),I))) ) ) ).

%----Get
tff(get_l,type,
    get_l: ( list * $int ) > pair ).

tff(get_nil,axiom,
    ! [I: $int] : ( get_l(nil,I) = null ) ).

tff(get_from_list,axiom,
    ! [L: list,I: $int] :
      ( ( L != nil )
     => ( get_l(L,I) = $ite(get_left(head(L)) = I,head(L),get_l(tail(L),I)) ) ) ).

%----Lemmas

%----Could not be proven, this would likely benefit from lists being data types by rewriting in SMT-LIB 2
tff(get_key,lemma,
    ! [L: list,I: $int] :
      ( ( get_l(L,I) != null )
     => ( get_left(get_l(L,I)) = I ) ) ).
